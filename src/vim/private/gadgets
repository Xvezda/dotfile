" Copyright (C) 2019 Xvezda <https://xvezda.com/>


" Markdown underlines function
" https://github.com/christoomey/your-first-vim-plugin

function! UnderlineHeading(level) abort
  if a:level == 1
    normal! yypVr=
  elseif a:level == 2
    normal! yypVr-
  else
    silent exec "normal! I" . repeat("#", a:level) . " "
  endif
endfunction

for level in range(1, 6)
  silent execute 'nnoremap <leader>u' . level
        \. ' :call UnderlineHeading(' . level . ')<CR>'
endfor


" Prevent writing typo file
function! s:check_filename_valid(name)
  " If filename is too short and only with non-word characters
  if !empty(a:name) && strlen(a:name) < 2 && a:name =~# '\W'
    return 0
  endif
  return 1
endfunction

augroup abort_short_filename
  autocmd!
  " expand('<afile>') -> new name
  " expand('%') -> original name
  autocmd BufWritePre *
        \ if !s:check_filename_valid(expand('<afile>')) |
        \   if !filereadable(expand('%'))
        \       && !s:check_filename_valid(expand('%')) |
        \     silent execute '0f' |
        \   endif |
        \   throw 'filename is too short' |
        \ endif
augroup END


" vim:ft=vim
