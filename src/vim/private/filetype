" Copyright (C) 2019 Xvezda <https://xvezda.com/>

" Enable doxygen syntax load
let g:load_doxygen_syntax = 1

" Set syntastic checker as passive
let g:syntastic_mode_map = {'mode':'passive'}

" Detect XE html template by its contents
augroup htmlxe_filetype_detect
  autocmd!
  autocmd FileType html
        \ let pattern = '\C\(' .
        \    '<load\s\+\(cond=".\+"\s*\|target="\(\w\|\.\|\-\|/\)\+"\s*\)/\?>' .
        \    '\|<!\-\-@\(if\|for\(each\)\?\|while\)\s*(.*)\?\-\->' .
        \    '\|{@\?\(\s\|\_.\)*\(\$\w\|Context::\w(\_.\+)\)\(\s\|\_.\)*}' .
        \    '\|<block\s\+cond=".\+">\_.*</block>' .
        \ '\)'
        \ | if getline(1).getline(2).getline(3).getline(4).getline(5) =~# pattern
        \ |   set ft=htmlxe
        \ | endif
        \ | unlet pattern
augroup END

" Detect React.js template by its contents
augroup reactjs_filetype_detect
  autocmd!
  autocmd FileType javascript
        \ let pattern = '\C^\(' .
        \    'import\(\s\(\[\s\w{}\],\)\+\)\?\(\sReact\)' .
        \    '\|\s\+render()\s\*{\?' .
        \    '\|\s\+ReactDOM' .
        \    '\|\s\+return\s\+(\s\*<' .
        \ '\)'
        \ | if getline(1).getline(2).getline(3).getline(4).getline(5) =~# pattern
        \ |   set ft=javascriptreact
        \ | endif
        \ | unlet pattern
augroup END

" Cutsom highlighting for C, C++
augroup c_filetype_detect
  autocmd!
  autocmd FileType c,cpp
        \ syntax match customType /\<[_[:alpha:]][[:alnum:]]*_t\>/
        \ | hi def link customType Type
augroup END

" Accept comment on tsconfig and windows terminal profile file
augroup config_json_filetype_detect
  autocmd!
  autocmd FileType json
        \ if expand("%:p") =~# '\C\([\\\/]tsconfig\.json' .
        \ '\|[\\\/]LocalState[\\\/]profiles\.json\)$'
        \ | let g:vim_json_warnings = 0
        \ | syntax clear jsonStringSQError
        \ | syntax region jsonComment start=+/\*+ end=+\*/+
        \ | syntax match jsonComment "//.*"
        \ | hi def link jsonComment Comment
        \ | endif
augroup END


" Custom highlighting for python
augroup python_filetype_detect
  autocmd!
  autocmd FileType python
        \ syntax match pythonConstant /\<[_[:upper:]][_[:upper:][:digit:]]\+\>/
        \ | hi def link pythonConstant Constant
        \ | syntax match pythonClass /\<\(_\?[[:upper:]][_[:lower:][:digit:]]\+\)\+\>/
        \ | hi def link pythonClass Type
        \ | let g:sleuth_automatic = 0
augroup END


" vim:ft=vim
